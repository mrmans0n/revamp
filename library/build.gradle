apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'


version '1.1.0-SNAPSHOT'
group 'io.nlopez.revamp'

dependencies {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:support-annotations:26.1.0'
    compile 'net.jodah:typetools:0.4.8'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.4'
    testCompile 'org.robolectric:robolectric-shadows:3.1'
    testCompile 'org.mockito:mockito-core:2.4.2'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 20005
        versionName version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

android.libraryVariants
publishing {
    publications {
        maven(MavenPublication) {
            artifact "build/libs/Revamp-${version}.aar"
            artifactId 'library'
        }
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getProperty(project, property) {
    return project.hasProperty(property) ? project.property(property) : ""
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: getProperty(project, "sonatypeRepo")) {
            authentication(userName: getProperty(project, "sonatypeUsername"),
                    password: getProperty(project, "sonatypePassword"))
        }
        snapshotRepository(url: getProperty(project, "sonatypeSnapshotRepo")) {
            authentication(userName: getProperty(project, "sonatypeUsername"),
                    password: getProperty(project, "sonatypePassword"))
        }

        pom.project {
            name 'Revamp'
            packaging 'aar'
            description 'Tiny MVP framework for Android'
            url 'https://github.com/mrmans0n/revamp'
            artifactId 'library'

            scm {
                url 'scm:git@github.com:mrmans0n/revamp.git'
                connection 'scm:git@github.com:mrmans0n/revamp.git'
                developerConnection 'scm:git@github.com:mrmans0n/revamp.git'
            }

            licenses {
                license {
                    name 'MIT license'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'mrmans0n'
                    name 'Nacho Lopez'
                    email 'nacho@nlopez.io'
                }
            }
        }
    }
}

repositories {
    google()
}